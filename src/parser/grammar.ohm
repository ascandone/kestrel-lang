Program {
  MAIN
    = (Declaration | TypeDeclaration)*

  // --- Toplevel
  Declaration
    = "let" ident (":" Type)? "=" Exp  -- letStmt

  TypeDeclaration
    = "type" typeName ("<" NonemptyListOf<typeVar, ","> ">")? "{" ListOf<TypeVariant, ","> ","? "}" -- typeDef

  // --- Expressions

  Exp
    = CompExp

  CompExp
    = CompExp "<" CompExp -- lt
    | CompExp "<=" CompExp -- lte
    | CompExp ">" CompExp -- gt
    | CompExp ">=" CompExp -- gte
    | EqExpr

  EqExpr
    = EqExpr "==" EqExpr -- eq
    | EqExpr "!=" EqExpr -- neq
    | AndExpr

  AndExpr
    = AndExpr "&&" AndExpr -- and
    | OrExpr

  OrExpr
    = OrExpr "||" OrExpr -- or
    | AddExp

  AddExp
    = AddExp "+" MulExp  -- plus
    | AddExp "-" MulExp  -- minus
    | MulExp

  MulExp
    = MulExp "*" ExpExp  -- times
    | MulExp "/" ExpExp  -- divide
    | MulExp "%" ExpExp  -- rem
    | ExpExp

  ExpExp
    = PriExp "^" ExpExp  -- power
    | PriExp

  PriExp
    = "(" Exp ")"  -- paren
    | PriExp "(" ListOf<Exp, ","> ","? ")" -- apply
    | "{" BlockContent "}"  -- block
    | "fn" ListOf<ident, ","> "{" BlockContent "}"  -- fn
    | "if" Exp "{" BlockContent "}" "else" "{" BlockContent "}" -- if
    | "match" Exp "{" ListOf<MatchClause, ","> ","? "}" -- match
    | ident -- ident
    | typeConstructor -- constructor
    | number

  BlockContent
    = Exp  -- exp
    | "let" ident "=" Exp ";" BlockContent -- let

  ident
    = ~keyword identHead identBody*

  typeConstructor
    = upper identBody*

  identHead
    = lower
    | "_"
  
  identBody
    = alnum
    | "_"

  number  (a number)
    = digit* "." digit+  -- fract
    | digit+             -- whole
    | "-" number  -- neg

  keyword = "let" | "fn" | "if" | "else"

  // --- Pattern matching

  MatchClause
    = MatchExpr "=>" Exp -- clause

  MatchExpr
    = ident  -- ident
    | typeConstructor ("(" ListOf<MatchExpr, ","> ")")? -- constructor

  // --- Types

  TypeVariant
    = typeName ("(" ListOf<Type, ","> ")")?

  Type
    = typeName ("<" ListOf<Type, ","> ">")?  -- named
    | typeVar -- var
    | "Fn" "(" ListOf<Type, ","> ")" "->" Type -- fn
    | "_" -- any

  typeName
    = ~"Fn" typeNameHead typeNameBody*

  typeNameHead
    = upper

  typeNameBody
    = letter

  typeVar
    = ~keyword typeVarHead typeVarBody*

  typeVarHead
    = lower
  
  typeVarBody
    = lower
    | digit

}
