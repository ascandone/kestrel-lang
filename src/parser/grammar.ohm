Program {
  MAIN
    = (Declaration | TypeDeclaration)*

  // --- Toplevel
  Declaration
    = "let" ident (":" Type)? "=" Exp  -- letStmt
    | "extern" "let" (ident | infixIdent) ":" Type -- externLetStmt

  TypeDeclaration
    = "type" typeName ("<" NonemptyListOf<typeVar, ","> ">")? "{" ListOf<TypeVariant, ","> ","? "}" -- typeDef
    | "extern" "type" typeName ("<" NonemptyListOf<typeVar, ","> ">")? -- externType

  // --- Expressions

  Exp
    = PipeExp

  PipeExp
    = PipeExp "|>" ident "(" ListOf<Exp, ","> ","? ")"  -- pipe
    | CompExp

  CompExp
    = CompExp "<" CompExp -- lt
    | CompExp "<=" CompExp -- lte
    | CompExp ">" CompExp -- gt
    | CompExp ">=" CompExp -- gte
    | EqExpr

  EqExpr
    = EqExpr "==" EqExpr -- eq
    | EqExpr "!=" EqExpr -- neq
    | AndExpr

  AndExpr
    = AndExpr "&&" AndExpr -- and
    | OrExpr

  OrExpr
    = OrExpr "||" OrExpr -- or
    | AddExp

  AddExp
    = AddExp "+" AddExp  -- plus
    | AddExp "+." AddExp  -- plusFloat
    | AddExp "-" AddExp  -- minus
    | AddExp "-." AddExp  -- minusFloat
    | AddExp "<>" AddExp  -- concat
    | MulExp

  MulExp
    = MulExp "*" MulExp  -- mult
    | MulExp "*." MulExp  -- multFloat
    | MulExp "/" MulExp  -- divide
    | MulExp "/." MulExp  -- divideFloat
    | MulExp "%" MulExp  -- rem
    | ExpExp

  ExpExp
    = ExpExp "^" ExpExp  -- power
    | NotExp

  NotExp
    = "!" NotExp -- not
    | PriExp

  PriExp
    = "(" Exp ")"  -- paren
    | "(" Exp "," NonemptyListOf<Exp, ","> ")"  -- tuple // Tuples have 2+ elements
    | PriExp "(" ListOf<Exp, ","> ","? ")" -- apply
    | "{" BlockContent "}"  -- block
    | "fn" ListOf<ident, ","> "{" BlockContent "}"  -- fn
    | "if" Exp "{" BlockContent "}" "else" "{" BlockContent "}" -- if
    | "match" Exp "{" ListOf<MatchClause, ","> ","? "}" -- match
    | ident -- ident
    | typeConstructor -- constructor
    | lit -- literal

  lit
    = number
    | string

  BlockContent
    = Exp  -- exp
    | "let#" ident ident "=" Exp ";" BlockContent -- monadicLet
    | "let" ident "=" Exp ";" BlockContent -- let

  ident
    = ~keyword identHead identBody*

  typeConstructor
    = upper identBody*

  identHead
    = lower
    | "_"
  
  identBody
    = alnum
    | "_"

  number
    = digit* "." digit+  -- fract
    | digit+             -- whole
    | "-" number  -- neg

  stringDelimiter = "\""
  string
    = stringDelimiter (~stringDelimiter any)* stringDelimiter

  keyword = "let" | "fn" | "if" | "else"

  // --- Pattern matching

  MatchClause
    = MatchPattern "=>" Exp -- clause

  MatchPattern
    = ident  -- ident
    | lit -- lit
    | "(" MatchPattern "," NonemptyListOf<MatchPattern, ","> ")" -- tuple
    | typeConstructor ("(" ListOf<MatchPattern, ","> ")")? -- constructor

  // --- Types

  TypeVariant
    = typeName ("(" ListOf<Type, ","> ")")?

  Type
    = typeName ("<" ListOf<Type, ","> ">")?  -- named
    | typeVar -- var
    | "Fn" "(" ListOf<Type, ","> ")" "->" Type -- fn
    | "_" -- any

  typeName
    = ~"Fn" typeNameHead typeNameBody*

  typeNameHead
    = upper

  typeNameBody
    = alnum

  typeVar
    = ~keyword typeVarHead typeVarBody*

  typeVarHead
    = lower
  
  typeVarBody
    = lower
    | digit
  
  infixIdent
    = "(" (~")" infixLetter)* ")"

  infixLetter
    = "+"
    | "-"
    | "*"
    | "/"
    | "^"
    | "="
    | ">"
    | "<"
    | "."
    | ":"
    | "!"
    | "%"
    | "&"
    | "|"
}
